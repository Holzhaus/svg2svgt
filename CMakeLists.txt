CMAKE_MINIMUM_REQUIRED(VERSION 2.8.11)

# Project properties
PROJECT(svg2svgt)
SET(SVG2SVGT_VERSION 0.9.6)


# Soname
# MAJOR is incremented when symbols are removed or changed in an incompatible way
# MINOR is incremented when new symbols are added
SET(SVG2SVGT_MAJOR 0)
SET(SVG2SVGT_MINOR 0)


# Variables
SET(LIB_INSTALL_DIR lib${LIB_SUFFIX} CACHE PATH "Library installation dir")
SET(INCLUDE_INSTALL_DIR include CACHE PATH "Header installation dir")

STRING(REGEX REPLACE "^${CMAKE_INSTALL_PREFIX}/" "" PC_INCLUDE_DIR ${INCLUDE_INSTALL_DIR})
STRING(REGEX REPLACE "^${CMAKE_INSTALL_PREFIX}/" "" PC_LIB_DIR ${LIB_INSTALL_DIR} )


# Dependencies
FIND_PACKAGE(Qt5Widgets REQUIRED)
FIND_PACKAGE(Qt5Xml REQUIRED)
FIND_PACKAGE(Qt5Svg REQUIRED)
FIND_PACKAGE(Qt5LinguistTools REQUIRED)

SET(CMAKE_AUTOMOC ON)

STRING(TOLOWER "${CMAKE_BUILD_TYPE}" CMAKE_BUILD_TYPE_TOLOWER)
IF(NOT CMAKE_BUILD_TYPE_TOLOWER MATCHES "debug")
    ADD_DEFINITIONS(-DQT_NO_DEBUG_OUTPUT)
ENDIF()


# Subdirectories
ADD_SUBDIRECTORY(svg2svgt)
ADD_SUBDIRECTORY(cli)
ADD_SUBDIRECTORY(gui)


# Translations
FILE(GLOB svg2svgt_TS locale/*.ts)
QT5_ADD_TRANSLATION(svg2svgt_QM ${svg2svgt_TS})
ADD_CUSTOM_TARGET(translations ALL DEPENDS ${svg2svgt_QM})
INSTALL(FILES ${svg2svgt_QM} DESTINATION share/svg2svgt)


# Tests
ADD_SUBDIRECTORY(test)


# Dist
ADD_CUSTOM_TARGET(dist
    COMMAND git archive --format=tar --prefix=${CMAKE_PROJECT_NAME}-${SVG2SVGT_VERSION}/ HEAD | xz > ${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}-${SVG2SVGT_VERSION}.tar.xz
    WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
)
